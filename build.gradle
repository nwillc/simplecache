/*
 * Copyright (c) 2015, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        jcenter {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://maven.tmatesoft.com/content/repositories/releases/'
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.nwillc:vplugin:1.2.4",
                "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4",
                'au.com.ish.gradle:release:2.2.2',
                'org.tmatesoft.sqljet:sqljet:1.1.9'
    }
}

apply plugin: 'release'
apply plugin: "com.github.nwillc.vplugin"
apply plugin: 'java'
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "jacoco"

repositories {
    jcenter {
        url 'http://jcenter.bintray.com'
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12',
            'javax.cache:cache-api:1.0.0'

    testCompile 'junit:junit:4.12',
            'org.assertj:assertj-core:3.2.0'
}

group = "com.github.nwillc"
version = "0.5"
targetCompatibility = "1.8"
sourceCompatibility = "1.8"

test {
    beforeTest { descriptor ->
        logger.lifecycle("\tRunning " + descriptor.className + "." + descriptor.name)
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: " + result.testCount
                    + ", Failures: " + result.failedTestCount
                    + ", Skipped: " + result.skippedTestCount)
        }
    }

    testLogging {
        exceptionFormat "full"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

release {
    failOnSnapshotDependencies = true
    allowLocalModifications = true
    releaseDryRun = false
    scm = 'git'
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'simplecache'
        desc = 'JSR 107 inspired simple caching'
        websiteUrl = 'https://github.com/nwillc/simplecache'
        issueTrackerUrl = 'https://github.com/nwillc/simplecache/issues'
        vcsUrl = 'https://github.com/nwillc/simplecache.git'
        licenses = ['ISC']
        labels = ['simplecache']
        publicDownloadNumbers = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/Shields**'])
        })
    }
}
